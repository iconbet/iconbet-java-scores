version = '0.1.0'

dependencies {
    compileOnly 'foundation.icon:javaee-api:0.9.1'

    implementation 'com.github.sink772:minimal-json:0.9.7'
    implementation 'foundation.icon:javaee-scorex:0.5.2'

    testImplementation 'foundation.icon:javaee-unittest:0.9.4'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

	testImplementation project(':tap-token')
	testImplementation project(':testinteg')
	testImplementation 'foundation.icon:icon-sdk:2.1.0'
    testImplementation('org.mockito:mockito-inline:4.6.1')

}

optimizedJar {
    mainClassName = 'com.iconbet.score.reward.RewardDistribution'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

deployJar {
    endpoints {
        local {
            uri = 'http://localhost:9082/api/v3'
            nid = 0x3
        }
        berlin {
            uri = 'https://berlin.net.solidwallet.io/api/v3'
            nid = 0x7
            to = 'cxe47dbc344a0f702303f7fd8e351791fd8a36a822'
        }
    }
    keystore = rootProject.hasProperty('keystoreName') ? "$keystoreName" : ''
    password = rootProject.hasProperty('keystorePass') ? "$keystorePass" : ''
}

test {
    useJUnitPlatform()
}

sourceSets {
	integrationTest {
		java.srcDirs = [file('src/intTest/java')]

		compileClasspath += sourceSets.main.output + configurations.testRuntime
		runtimeClasspath += output + compileClasspath
	}
}

configurations {
  integrationTestCompile.extendsFrom testImplementation
  integrationTestRuntime.extendsFrom testRuntime
  integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

def tapTokenJar = project(':tap-token').getTasks().getByName('optimizedJar')

task integrationTest(type: Test, dependsOn: optimizedJar) {
	useJUnitPlatform()
	description = 'Runs integration tests.'
	group = 'verification'

	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	testLogging.showStandardStreams = true

	// use the common config files
	systemProperty('env.props', new File(project(':testinteg').projectDir, 'conf/env.props'))

	def prefix = 'score.path.'
	systemProperty(prefix + project.name, optimizedJar.outputJarName)
	systemProperty(prefix + 'tap-token', tapTokenJar.outputJarName.substring(0, tapTokenJar.outputJarName.lastIndexOf("-optimized.jar") )+ "-"+project.version+"-optimized.jar" )

}

integrationTest.dependsOn(tapTokenJar)
