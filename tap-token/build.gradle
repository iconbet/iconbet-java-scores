version = '0.1.0'

dependencies {
    compileOnly 'foundation.icon:javaee-api:0.9.0'
    //needed to check why this lib is needed
    implementation 'com.github.sink772:javaee-tokens:0.6.0'

    testImplementation 'foundation.icon:javaee-unittest:0.9.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
    testImplementation 'foundation.icon:icon-sdk:2.0.0'
	testImplementation project(':testinteg')
}

optimizedJar {
    mainClassName = 'com.iconbet.score.tap.TapToken'
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

deployJar {
    endpoints {
        local {
            uri = 'http://localhost:9082/api/v3'
            nid = 0x3
        }
        berlin {
            uri = 'https://berlin.net.solidwallet.io/api/v3'
            nid = 0x7
            to = 'cx759ed3fa7d932447939ee19e0ff3490ba1c65ab0'
        }
    }
    keystore = rootProject.hasProperty('keystoreName') ? "$keystoreName" : ''
    password = rootProject.hasProperty('keystorePass') ? "$keystorePass" : ''
	parameters {
		arg('_initialSupply', '100')
		arg('_decimals', '6')
	}
}

test {
    useJUnitPlatform()
}


sourceSets {
	integrationTest {
		java.srcDirs = [file('src/intTest/java')]

		compileClasspath += sourceSets.main.output + configurations.testRuntime
		runtimeClasspath += output + compileClasspath
	}
}

configurations {
  integrationTestCompile.extendsFrom testImplementation
  integrationTestRuntime.extendsFrom testRuntime
  integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

task integrationTest(type: Test, dependsOn: optimizedJar) {
	useJUnitPlatform()
	description = 'Runs integration tests.'
	group = 'verification'

	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	testLogging.showStandardStreams = true

	// use the common config files
	systemProperty('env.props', new File(project(':testinteg').projectDir, 'conf/env.props'))

	def prefix = 'score.path.'
	systemProperty(prefix + project.name, optimizedJar.outputJarName)

}
